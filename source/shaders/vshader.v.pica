; Uniforms
.fvec projection[4], modelview[4]

; Constants
.constf myconst(0.0, 1.0, -1.0, 0.0)
.alias zeros myconst.xxxx
.alias ones myconst.yyyy
.alias negones myconst.zzzz
.alias dummytcoord myconst.xxxy ; (0,0,0,1)

; Outputs
.out outpos position
.out outtc0 texcoord0
.out outtc1 texcoord1
.out outtc2 texcoord2
.out outclr color

; Inputs
.alias inpos v0
.alias inclr v1
.alias intc0 v2
.alias intc1 v3

.proc main
	; r0 = (inpos.x, inpos.y, inpos.z, 1.0)
	mov r0.xyz, inpos
	mov r0.w, ones
	
	; r1 = modelview * r0
	dp4 r1.x, modelview[0], r0
	dp4 r1.y, modelview[1], r0
	dp4 r1.z, modelview[2], r0
	dp4 r1.w, modelview[3], r0
	
	; outpos = projection * r1
	dp4 outpos.x, projection[0], r1
	dp4 outpos.y, projection[1], r1
	dp4 outpos.z, projection[2], r1
	dp4 outpos.w, projection[3], r1
	
	; Set texcoords
	mov outtc0, intc0
	mov outtc1, intc1
	mov outtc2, dummytcoord
	
	; Set vertex color
	mov outclr.xyzw, inclr
	
	end
.end
